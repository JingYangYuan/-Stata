# 因果推断实用计量方法
## 作者：邱嘉平
### 框定代码区域

```stata
global libname "enter your path here"
```

## 第三章

```stata
use "$libname\data3.dta", clear

describe re78 treat age education black hispanic married nodegree re74 re75 un74 un75

summarize re78 treat age education black hispanic married nodegree re74 re75 un74 un75

tabstat re78 treat age education black hispanic married nodegree re74 re75 un74 un75, statistics(n mean median min p25 p75 max) format(%-7.4g) column(statistics)

summarize age, detail

correlate re78 treat age education black hispanic married nodegree re74 re75 un74 un75

regress re78 treat age education black hispanic married nodegree re74 re75 un74 un75

test black = hispanic

test married nodegree

reg re78 treat age black hispanic married nodegree re74 re75 un74 un75

estimate store reg1, title(regression 1)

reg re78 treat education black hispanic married nodegree re74 re75 un74 un75

estimate store reg2, title(regression 2)

esttab reg1 reg2, b(%7.3f) se(%7.3f) stat(N r2 F) title ("回归结果")
```


## 第四章

```stata
clear all
set seed 10000

quiet set obs 200
gen x = 5 * rnormal(0,1)
gen v = 5 * rnormal(0,1)
gen w = exp(-0.5 + 0.2 * x)
gen e = w * v
gen y = 5 + 3 * x + e

reg y x

gen one_star = 1 / w
gen y_star = y / w
gen x_star = x / w

reg y_star x_star one_star, noconstant

clear all
set seed 10000
quiet set obs 200
gen time = _n
tsset time
gen x = 0 in 1/1
replace x = 0.4 * l.x + rnormal(0,5) in 2/200
gen v = rnormal(0,5)
gen err = 0 in 1/1
scalar pho = 0.8
replace e = pho * l.e + v in 2/200
gen y = 5 + 3 * x + e

reg y x
newey y x, lag(1)

gen one = 1
gen one_star = one - pho * l.one
gen y_star = y - pho * l.y
gen x_star = x - pho * l.x
reg y_star x_star one_star, noconstant

use "$libname\data4.dta", clear

loneway score school

sum score

mean score

reg score

reg score, cluster(school)
```


## 第五章

```stata
use "$libname\data5.dta", clear

describe stdntid g1schid whiteasian age1985 female g1classtype g1classsize score

drop if g1classtype == .

bysort g1classtype: sum whiteasian female g1classsize age1985

gen small = (g1classtype == 1)
gen regular = (g1classtype == 2)
gen regularaid = (g1classtype == 3)
reg score small regularaid

bysort g1classtype: sum score

xi: reg score small regularaid i.g1schid

xi: reg score small regularaid female whiteasian age1985 i.g1schid

xi: reg score small regularaid female whiteasian age1985 1.g1schid, cluster(g1schid)
```


## 第六章

```stata
use "$libname\data6.dta", clear

tabstat age education black hispanic married nodegree re74 re75, statistics(mean) by(treat)

pscore treat age education black hispanic married nodegree re74 re75 un74 un75, logit blockid(block) pscore(myscore) comsup

drop myscore block

gen age2 = age ^ 2
gen age3 = age ^ 3
gen education2 = education ^ 2
gen educationre74 = education * re74
gen re742 = re74 ^ 2
gen re752 = re75 ^ 2

pscore treat age age2 age3 education education2 black hispanic married nodegree re74 re742 re75 re752 un74 un75 educationre74, logit blockid(block) pscore(myscore)

attnd re78 treat, pscore(myscore)

/*这个命令紧接着 pscore 使用
它调用了 pscore 命令里计算并存储在 myscore 变量中的倾向得分，
也可以单独使用以下命令:

attnd re78 treat age age2 age3 education education2 black hispanic married nodegree re74 re742 re75 re752 un74 un75, logit

*/

attnd re78 treat, pscore(myscore) comsup

attnd re78 treat, pscore(myscore) boot reps(100) comsup

psmatch2 treat, pscore(myscore) outcome(re78) neighbor(1) ties

/*调用了 pscore命令里计算并存储在 myscore 变量中的倾向得分，也可以直接使用以下命令

psmatch2 treat age age2 age3 education education2 black hispanic married nodegree re74 re742 re75 re752 un74 un75 educationre74,logit outcome(re78) neighbor(1) ties

*/
drop myscore block

pscore treat age age2 age3 education education2 black hispanic married nodegree re74 re742 re75 re752 un74 un75 educationre74, logit blockid(block) pscore(myscore) comsup

attnd re78 treat, pscore(myscore) boot reps(100) comsup
psmatch2 treat if comsup == 1, pscore(myscore) outcome(re78) neighbor(1) ties

bootstrap r(att), reps(100): psmatch2 treat age age2 age3 education education2 black hispanic married nodegree re74 re742 re75 re752 un74 un75 educationre74, logit outcome(re78) neighbor(1) common ties

teffects psmatch (re78) (treat age age2 age3 education education2 black hispanic married nodegree re74 re742 re75 re752 un74 un75 educationre74, logit), atet nneighbor(1)

pstest age education black hispanic married nodegree re74 re75, both
```


## 第七章

```stata
use "$libname\data7-1.dta", clear

gen DOAGE30 = (D == 0 & AGE == 30)
gen D1AGE30 = (D == 1 & AGE == 30)
gen DOAGE40 = (D == 0 & AGE == 40)
gen D1AGE40 = (D == 1 & AGE == 40)

list, noobs sepby (AGE)

reg Y DOAGE30 D1AGE30 DOAGE40 D1AGE40, noconstant

reg Y D1AGE30 DOAGE40 D1AGE40

gen AGE30 = (AGE == 30)
gen AGE40 = (AGE == 40)

reg Y D AGE30 AGE40, noconstant

use "$libname\data7-2.dta", clear

twoway (scatter inc1 iq if college == 0) (scatter inc1 iq if college == 1), xline(100)

reg inc1 college iq

reg inc2 college iq

use "$libname\data7-3.dta", clear

reg inc1 college iq
reg inc2 college iq
```


## 第八章

```stata
use "$libname\data8-1.dta", clear

xtset id year

xtsum

use "$libname\data8-2.dta", clear

xtset id year

twoway (line q year if id == 1) (line q year if id == 2) (line q year if id == 3) (line q year if id == 4), xline(2014) legend(order(1 "企业1" 2 "企业2" 3 "企业3" 4 "企业4"))

xtsum q



egen mq = mean(q), by(id) /*按id，生成个体 q 均值*/
egen mtax = mean(tax), by(id) /*按id，生成个体 tax 均值*/
generate within_q = q - mq /*对q进行去个体均值转换*/
generate within_tax = tax - mtax /*对tax进行去个体均值转换*/
reg within_q within_tax, noconstant /*将转换后的q和tax做回归，nonconstant设置没有共同截距*/

xtreg q tax, fe

generate dq = q - L.q // 产生q变量的一阶差
generate dtax = tax - L.tax // 产生tax变量的一阶差
regress dq dtax, noconstant //用一阶差变量回归，没有截距项

xi: xtreg q tax i.year, fe
```


## 第九章

```stata
use "$libname\data9.dta", clear

gen treat = id <= 2 //产生组虚拟变量：处置组=1,控制组=0

collapse (mean) q, by(year treat)

use "$libname\data9-1.dta", clear

twoway (line q year if treat == 1) (line q year if treat == 0), xline(2014) scheme(tufte) legend(order(1 "处理组" 2 "对照组"))

use "$libname\data9.dta", clear

gen treat = id <= 2 //产生组虚拟变量：处置组=1,控制组=0
gen after = year >= 2014 //产生时期虚拟变量：2014年和2014年后=1，2014年前=0

mean q if treat == 1 & after == 0

mean q if treat == 1 & after == 1

mean q if treat == 0 & after == 0

mean q if treat == 0 & after == 1

reg q treat if after == 1

reg q after if treat == 1

gen treatafter = after * treat //产生交叉变量
list id year q tax treat after treatafter, sep(8) ab(12) noobs //列出数据

reg q after treat treatafter

use "$libname\data9.dta", clear

gen treat = id <= 2 //产生组虚拟变量：处置组=1,控制组=0
gen after = year >= 2014 //产生时期虚拟变量：2014年和2014年后=1，2014年前=0
gen treatafter = after * treat //产生交叉变量

tab id, gen(id) //个体虚拟变量
tab year, gen(year) //年虚拟变量

reg q treatafter id1-id4 year2-year8, noconstant //LSDV回归，无常数项

use "$libname\data9.dta", clear

gen treat = id <= 2 //产生组虚拟变量：处置组=1.控制组=0
gen after1 = year == 2014 //产生年虚拟变量；2014年=1
gen after2 = year == 2015 //产生年虚拟变量：2015年-1
gen after3 = year > 2015 //产生年虚拟变量：2015年后=1
gen treatafter1 = after1 * treat //产生交叉变量1
gen treatafter2 = after2 * treat //产生交叉变量2
gen treatafter3 = after3 * treat //产生交叉变量3
tab id, gen(id) //个体虚拟变量
tab year, gen(year) //年虚拟变量
reg q treatafter1-treatafter3 id1-id4 year2-year8, noconstant //LSDV回归，无常数项

*双重差分法平行检验
use "$libname\data9.dta", clear

gen treat = id <= 2 //产生组虚拟变量：处置组=1.控制组=0
gen after = year >= 2014 //产生时期虚拟变量：2014年和2014年后=1,2014年前=0
gen treatafter = after * treat //产生组和时期交叉变量
tab year, gen(year) //年虚拟变量
gen treatyear2 = treat * year2 //组和2011年交叉变量
gen treatyear3 = treat * year3 //组和2012年交叉变量
gen treatyear4 = treat * year4 //组和2013年交叉变量
reg q treat treatyear2-treatyear4 after treatafter
```


## 第十章

```stata
use "$libname\data10.dta", clear

reg loggdp institutions latitude, robust

ivregress 2sls loggdp latitude (institutions = logmortality), first

estat endogenous

estat firststage

ivregress 2sls loggdp latitude (institutions = logmortality euro1900), first

estat overid
```


## 第十一章

```stata
*这个数据怪怪的，主要是书上也没说从哪下，从连享会哪里找到一个类似物

use "$libname\data11-1.dta", clear

list wage age education children if _n <= 20

gen work = (wage ~= .) /*当wage未缺失时，work=1,反之为0*/
probit work education age children /*估计D=1的概率*/

predict z if e(sample), xb /*用probit得到的系数计算Z*/
generate phi = normalden(2) /*计算对应的正态分布的概率密度值φ(2)*/
generate PHI = normal(2) /*计算对应的正态分布的累计分布值φ(Z)*/
generate lambda = phi / PHI /*计算逆米尔斯比例λ=φ(Z)/φ(Z)*/

list wage age education children z phi PHI lambda if _n <= 20

reg wage education age lambda

heckman wage education age, select (education age children) twostep

*另一个数据实在找不到了…………，从连享会搬了一个来将就看看

use "$libname\data11-2.dta", clear

*ols
reg wage age grade smsa black tenure union
est store OLS

*etregress maximum likelihood
etregress wage age grade smsa black tenure, ///
          treat(union = south black tenure) twostep 
est store etrMLE

*etregress two-step all-in-one  不可以进行cluster调整
etregress wage age grade smsa black tenure, ///
          treat(union = south black tenure) twostep 
est store etr2s	

*etregress two-step step-by-step 可以进行cluster调整
probit union south black tenure
est store First
predict y_hat, xb
gen pdf = normalden(y_hat)
gen cdf = normal(y_hat)
gen imr1 = pdf / cdf            //union = 1算法
gen imr2 = (-pdf) / (1 - cdf) //union = 0算法
gen imr = imr1 
replace imr = imr2 if union == 0
reg wage age grade smsa black tenure union imr
est store Second
vif

*对比结果
local m "OLS etrMLE etr2s First Second"
esttab `m', mtitle(`m') nogap compress pr2 ar2
```

## 十二章数据未开源，无从下手…………